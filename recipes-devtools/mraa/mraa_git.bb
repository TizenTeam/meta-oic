SUMMARY = "Low Level Skeleton Library for Communication on Intel platforms"
SECTION = "libs"
AUTHOR = "Brendan Le Foll, Tom Ingleby"

LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://COPYING;md5=e8db6501ed294e65418a933925d12058"

# git is required to get a good version from git describe
SRCREV="ea183b37388d96d51ab8cb64712259e86a03c465"
SRC_URI = "git://github.com/intel-iot-devkit/mraa.git;protocol=git"

S = "${WORKDIR}/git"

inherit distutils-base pkgconfig python-dir cmake

FILES_${PN}-doc += "${datadir}/mraa/examples/"

FILES_${PN}-dbg += "${PYTHON_SITEPACKAGES_DIR}/.debug/"

EXTRA_OECMAKE  += " -DBUILDSWIGNODE=OFF "


do_compile_prepend () {
  # when yocto builds in ${D} it does not have access to ../git/.git so git
  # describe --tags fails. In order not to tag our version as dirty we use this
  # trick
  sed -i 's/-dirty//' src/version.c
}
